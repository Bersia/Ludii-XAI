(define "Columns" 8)
(define "NextHole" ("NextSiteOnTrack" #3 from:#1 #2))

//------------------------------------------------------------------------------

(game "Mweso Exhibition" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" "16,E,N1,W" loop:True P2)
            (track "TrackCW1" "7,W,N1,E" loop:True P1)
            (track "TrackCW2" "24,E,S1,W" loop:True P2)
            }
        )
        (piece "Seed" Shared)
        (regions "Home" P1 (sites Track "TrackCCW1"))          // P1 Home
        (regions "Home" P2 (sites Track "TrackCCW2"))          // P2 Home
        (regions "Inner" P1 (difference (sites Track "TrackCCW1") (sites Bottom)))   // P1 Inner Home
        (regions "Inner" P2 (difference (sites Track "TrackCCW2") (sites Top)))   // P2 Inner Home
        (regions "Left" P1 (intersection (sites Track "TrackCCW1") (expand (sites Left)))) // P1 Left Holes
        (regions "Left" P2 (intersection (sites Track "TrackCCW2") (expand (sites Right)))) // P2 Left Holes
        (hand Each)
    }) 
    (rules 
        
        (start (set Count 2 to:(sites Board)))
        
        (play
            (or {
                (if (!= 2 (var "Direction"))
                    (move Select
                        (from 
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                (sites Mover "Home") 
                            )
                            if:(< 1 (count at:(from)))
                        )
                        (then
                            (sow
                                "TrackCCW"
                                owner:(mover)
                                apply:(if (< 1 (count at:(to)))
                                    (and {
                                        (set Var "Direction" 1)
                                        (moveAgain)
                                        (set Var "Replay" (to))
                                        (if (is In (to) (sites Mover "Inner"))
                                            (if 
                                                (and
                                                    (is Occupied ("OppositePit" (to)))
                                                    (is Occupied ("OppositeOuterPit" (to)))
                                                )	
                                                (and {
                                                    (fromTo
                                                        (from ("OppositePit" (to)))
                                                        (to (last From))
                                                        count:(count at:("OppositePit" (to)))
                                                    )
                                                    (fromTo
                                                        (from ("OppositeOuterPit" (to)))
                                                        (to (last From))
                                                        count:(count at:("OppositeOuterPit" (to)))
                                                    )
                                                    (sow 
                                                        (last From)
                                                        count:(+ (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to))))
                                                        "TrackCCW"
                                                        owner:(mover)
                                                    )
                                                })
                                            )
                                        )
                                    })
                                    (set Var "Direction" 0)
                                )
                            )
                        )
                    )
                )
                (if (!= 1 (var "Direction"))
                    (move Select
                        (from 
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                (sites Mover "Left") 
                            )
                            if:(and
                                (< 1 (count at:(from)))
                                (if ("SameTurn")
                                    True
                                    (if (is In ("NextHole" (from) "TrackCW" (count at:(from))) (sites Mover "Inner"))
                                        (and
                                            (is Occupied ("OppositePit" ("NextHole" (from) "TrackCW" (count at:(from)))))
                                            (is Occupied ("OppositeOuterPit" ("NextHole" (from) "TrackCW" (count at:(from)))))
                                        )
                                        False
                                    )
                                )
                            )
                        )
                        (then
                            (sow
                                "TrackCW"
                                owner:(mover)
                                apply:(if (< 1 (count at:(to)))
                                    (and {
                                        (set Var "Direction" 2)
                                        (moveAgain)
                                        (set Var "Replay" (to))
                                        (if (is In (to) (sites Mover "Inner"))
                                            (if 
                                                (and
                                                    (is Occupied ("OppositePit" (to)))
                                                    (is Occupied ("OppositeOuterPit" (to)))
                                                )	
                                                (and {
                                                    (fromTo
                                                        (from ("OppositePit" (to)))
                                                        (to (last From))
                                                        count:(count at:("OppositePit" (to)))
                                                    )
                                                    (fromTo
                                                        (from ("OppositeOuterPit" (to)))
                                                        (to (last From))
                                                        count:(count at:("OppositeOuterPit" (to)))
                                                    )
                                                    (sow 
                                                        (last From)
                                                        count:(+ (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to))))
                                                        "TrackCCW"
                                                        owner:(mover)
                                                    )
                                                })
                                            )
                                        )
                                    })
                                    (set Var "Direction" 0)
                                )
                            )
                        )
                    )
                )
            })
        )
        
        (end (if (and (not (is Next Mover)) (no Moves Next)) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (rules "De här reglerna observerades i Uganda år 1912. >Plocka upp alla pjäser ur ett hål och släpp dem en och en i motsols riktning i hålen längs spelarens två rader. När den sista pjäsen landar i ett hål mittemot motspelarens två hål med pjäser i så fångas de. Samma spelare fortsätter spela tills den sista pjäsen landar i ett tomt hål. Spelaren som inte kan göra ett drag förlorar.")
        }
    )
    
    (graphics {
        (board Style Mancala)
        (board Placement offsetY:-0.3)
    })
    
)

